title: <span style="color: #000000;">Center Party</span> Conference
subtitle: The <span style="color: #000000;">Center Party</span> chooses their new leader...
view-if: year = 1928 and month = 12 and z_leader = "Marx"
max-visits: 1
tags: event
new-page: true

= <span style="color: #000000;">Center Party</span> Conference

# reference: Evans, Ellen L. “Adam Stegerwald and the Role of the Christian Trade Unions in the Weimar Republic.” The Catholic Historical Review 59, no. 4 (1974): 602–26.

Wilhelm Marx, former <span style="color: #000000;">Center Party</span> chancellor and presidential candidate, has chosen to resign from leadership of the <span style="color: #000000;">Center Party</span>. Now, the party holds a conference to replace him.

The consensus candidate on the right is Ludwig Kaas, a Catholic priest who represents the party's traditionalists. On the other hand, there is Joseph Joos, a leader of the Catholic trade unions, who has been more supportive of cooperation with the <span style="color: #c00000;">**SPD**</span> than most of the <span style="color: #000000;">Center Party</span>. [? if not prussia_leader == "Stegerwald": Christian labor union chairman Adam Stegerwald has fallen to a distant third, making it highly unlikely that he will win the leadership.?][? if prussia_leader == "Stegerwald": Christian labor union chairman and Minister-President Adam Stegerwald has lost some ground, but he retains strong support from the Prussian branch of the party, effectively securing him the nomination. ?]

- @kaas: The <span style="color: #000000;">Center Party</span> elects Ludwig Kaas as the party chair.
- @joos: Influence the <span style="color: #000000;">Center Party</span> to select Joseph Joos as the party chair.
- @joos_campaign: We must hold a campaign in support of Joseph Joos as party chair.
- @stegerwald: The <span style="color: #000000;">Center Party</span> elects Adam Stegerwald as the party chair.

@kaas
view-if: not prussia_leader == "Stegerwald"
on-arrival: z_leader = "Kaas"; z_relation -= 5; prussian_concordat_progress += 1; z_ideology = "Right"

Ludwig Kaas has been selected as the chairman of the <span style="color: #000000;">Center Party</span>. He seems to represent a rightward turn from Marx, and will preside over a <span style="color: #000000;">Center Party</span> less inclined to compromise with the left. 

Despite this, Kaas is committed to the current constitutional order and the republic. He maintains a close friendship with Eugenio Pacelli, the Papal Nuncio to Germany, which may prove useful during concordat negotiations.

@joos
unavailable-subtitle: Our relations are not good enough.
subtitle: Joos is likely to be friendlier to us than Kaas.
view-if: not prussia_leader == "Stegerwald"
choose-if: z_relation >= 70
on-arrival: z_leader = "Joos"; z_ideology = "Left"; catholics_z -= 3; catholics_dnvp += 2; catholics_other += 2; catholics_spd += 5; workers_z += 5; z_relation += 4; zentrum_disintegration += 1; dvp_left += 1; ddp_left += 1; ddp_cohesion += 1; lvp_left += 1

With our grassroots connections, we have been able to influence the <span style="color: #000000;">Center</span> delegates to support Joseph Joos as the new <span style="color: #000000;">Center</span> chairman. He has long been a friend of the <span style="color: #c00000;">**SPD**</span>, and will be more inclined to collaborate with us compared to Kaas. He also increases the appeal of the <span style="color: #000000;">Center Party</span> to the working class, though it comes at the cost of deepening the social and economic divisions once held together by religion.

@joos_campaign
choose-if: z_relation >= 60 and resources >= 1 
view-if: z_relation < 70 and not prussia_leader == "Stegerwald"
unavailable-subtitle: [? if z_relation <= 62 : Our relations are not good enough. ?] [? if resources < 1 : We do not have enough resources. ?]
subtitle: -1 resource. Joos is likely to be friendlier to us than Kaas.
on-arrival: resources -= 1; z_leader = "Joos"; z_ideology = "Left"; catholics_dnvp += 2; catholics_other += 2; catholics_spd += 5; workers_z += 5; z_relation += 4; zentrum_disintegration += 1; dvp_left += 1; ddp_left += 1; ddp_cohesion += 1; lvp_left += 1

With our grassroots connections as well as a professional campaign, we have been able to influence the <span style="color: #000000;">Center</span> delegates to support Joseph Joos as the new <span style="color: #000000;">Center</span> chairman. He has long been a friend of the <span style="color: #c00000;">**SPD**</span>, and will be more inclined to collaborate with us compared to Kaas. He also increases the appeal of the <span style="color: #000000;">Center Party</span> to the working class, though it comes at the cost of deepening the social and economic divisions once held together by religion.

@stegerwald
choose-if: prussia_leader == "Stegerwald"
unavailable-subtitle: Stegerwald lacks the influence necessary to have a real chance.
on-arrival: z_leader = "Stegerwald"; z_ideology = "Right"; stegerwald_path = 1; z_relation -= 5; zentrum_disintegration += 2; new_middle_z -= 5; old_middle_z -= 3; rural_z -= 1; catholics_z -= 5; workers_z += 5; workers_spd -= 3; unemployed_z += 5; catholics_spd += 8; catholics_other += 4; catholics_dnvp += 2 if dnvp_ideology == "Moderate"; catholics_dvp += 4; catholics_ddp += 2; catholics_lvp += 4; dvp_right += 1; ddp_left += 1; ddp_cohesion += 1; liberal_cooperation += 1; bourgeois_cooperation += 1; new_middle_dnvp -= 2 if dnvp_ideology == "Radical"; old_middle_dnvp -= 3 if dnvp_ideology == "Radical"; rural_dnvp -= 3 if dnvp_ideology == "Radical"; new_middle_z += 2 if dnvp_ideology == "Radical"; old_middle_z += 3 if dnvp_ideology == "Radical"; rural_z += 3 if dnvp_ideology == "Radical"; prussia_leader = "Brüning"

Adam Stegerwald has been elected as the chairman of the <span style="color: #000000;">Center Party</span>. He seems to represent a rightward turn from Marx, despite his trade union background, and will preside over a <span style="color: #000000;">Center Party</span> less inclined to compromise with the left. He views the <span style="color: #000000;">Center Party</span> as a prototype for a Christian, democratic, German, and social party, and aims to transform it according to the principles laid out in his proposed "Essen program" eight years prior. 

The <span style="color: #000000;">Center Party</span>'s middle class wing are in uproar over the vote, despite Stegerwald conceding his leadership of the Reichstag delegation and the Minister-Presidency to his protege Heinrich Brüning. [? if dnvp_ideology == "Radical": His election also attracts some Protestant <span style="color: #3E88B3;">**DNVP**</span> dissidents and industrialists, who may result in a shift of the party towards the right. ?]