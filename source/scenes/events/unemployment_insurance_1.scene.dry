title: Unemployment Insurance Crisis
subtitle: The right wing of our coalition presents an ultimatum.
new-page: true
view-if: spd_in_government = 1 and ((year = 1930 or year = 1931 or year = 1932 and month >= 3) or (year = 1930 and month >= 9 and prolonged_unemployment_insurance = 1)) and in_grand_coalition = 1 and unemployment_insurance_timer = 0 and (unemployed >= unemployment_insurance_threshold + 4 or (unemployed >= 20 and unemployed >= unemployment_insurance_threshold)) and not constructive_vonc and president = "Hindenburg" and not lvp_formed
on-arrival: unemployment_insurance_crisis += 1; budget -= 1 if budget >= 1; unemployment_insurance_timer = 10; crisis_urgency += 1; unemployment_insurance_threshold = unemployed
tags: event
priority: -1
max-visits: 2
go-to: what_do if unemployment_insurance_crisis = 2

= Unemployment Insurance Crisis

The economic crisis has caused mass layoffs all throughout Germany, affecting the industrial working class the hardest. Our welfare spending is increasing due to the greater need.

In the midst of all this, our right-wing coalition partners in the <span style="color: #C0A054;">**DVP**</span>[? if ddp_ideology == "Right" and not dstp_formed:, <span style="color: #D3C24D;">**[+ ddp_name +]**</span>?][? if dstp_formed and ddp_ideology == "Right" :, DStP?] and <span style="color: #000000;">Center</span> parties have a plan to cut benefits for the unemployed, lower taxes on the rich, and raise taxes on the working class, as part of a plan to balance the budget and discipline labor. They do not see a possibility for compromise. If we do not accept, they could blow up the coalition.

[? if unemployment_insurance_crisis = 2 : This is the second such ultimatum thrown by the Right. Obviously, the bourgeoisie are trying to remove us from the government. ?]

- @tax_rich: Why can't we simply tax the rich?
- @gift: This is a gift to the <span style="color: #700000;">Communists</span>.

@tax_rich
max-visits: 1
go-to: what_do

The bourgeois parties, as representatives of the rich industrialists, would not have that.

@gift
go-to: what_do

This is a gift to the <span style="color: #700000;">Communists</span>, a boost to their theory that parliamentary democracy will never benefit the working class.

@what_do
on-arrival: dvp_relation += 3 if chancellor = "Braun"; lvp_relation += 3 if chancellor = "Braun"; dvp_relation += 3 if in_grand_coalition_prussia

But the crux of the matter is, what can we do?

- @destroy: Fine. We will end this coalition.
- @accept: Unfortunately, we must accept the Right's demands for the sake of stability.
- @compromise: Surely there is some compromise to be had?
- @prolong: Postpone the issue as long as possible.

@destroy
on-arrival: spd_in_government = 0; grand_coalition_collapsed = 1 if in_grand_coalition; in_grand_coalition = 0; spd_toleration = 0; spd_toleration_right = 0; z_relation -= 8; dvp_relation -= 10; ddp_relation -= 6; chancellor = "Brüning"; chancellor_party = "Z"; welfare -= 2; budget += 1; inflation -= 2; foreign_minister_party = "DVP"; foreign_minister_party = "LVP" if lvp_formed; interior_minister_party = "Z"; justice_minister_party = "I"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = ddp_name; economic_minister_party = "LVP" if lvp_formed; finance_minister_party = "DVP"; finance_minister_party = "LVP" if lvp_formed; agriculture_minister_party = "Z"; spd_no_confidence = 1; pro_republic -= 4; old_middle_spd -= 3; new_middle_spd -= 2; rural_spd -= 2; catholics_spd -= 2; unemployment_insurance_gov_destroyed = 1; dvp_right += 3; ddp_right += 2; ddp_cohesion -= 1; grand_coalition_collapsed = 1; bruning_event_timer = 6 if not bruning_event_seen and year >= 1930; economic_growth -= 0.6
go-to: elections if bruning_event_seen or year < 1930

Fine. If the bourgeoisie wish to blow up the government, let them do so. <span style="color: #c00000;">Socialists</span> will not be complicit in the destruction of the working class.

[? if chancellor_party = "SPD" : Heinrich Brüning of the <span style="color: #000000;">Center Party</span> has been appointed Chancellor. ?] Brüning now leads an all-bourgeois cabinet. Using the emergency powers of Article 48, Brüning and President Hindenburg have passed the welfare cuts over the objections of the Reichstag.

[? if not bruning_event_seen and year >= 1930: New elections haven't been called yet but it's likely that they will be soon, considering Brüning most likely doesn't have the majority of the Reichstag behind him. ?]

@elections
call: set_next_election_time

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@accept
unavailable-subtitle: [? if reformist_strength + center_strength < left_strength + labor_strength: The party won't allow this. ?]
choose-if: reformist_strength + center_strength >= left_strength + labor_strength
on-arrival: workers_spd -= 0.15*workers_spd; unemployed_spd -= 0.3*unemployed_spd; workers_kpd += 5; workers_nsdap += 6; unemployed_kpd += 5; unemployed_nsdap += 5; left_dissent += 15; center_dissent += 10; labor_dissent += 15; reformist_dissent += 5; welfare -= 2; budget += 1; unemployed += 2; pro_republic -= 4; kpd_relation -= 8; inflation -= 2; dvp_left += 1; ddp_cohesion += 1; ddp_right += 1; goal_spd_cancel += 1; economic_growth -= 0.6

Unfortunately, we must accept this ultimatum if we are to maintain this coalition. The <span style="color: #7A3C00;">Nazis</span> and <span style="color: #700000;">**Communists**</span> are waiting in the wings for us to fail, ready to pounce upon the instability that would result from a breakup of the governing coalition.

The workers are furious at our betrayal, as are a good portion of our membership throughout the factions. Our loss is the <span style="color: #700000;">**KPD**</span>'s gain, and even the <span style="color: #7A3C00;">**NSDAP**</span>, those lapdogs of the bourgeoisie, have joined in criticizing us for our austerity.

@prolong
unavailable-subtitle: [? if reformist_strength + center_strength < left_strength + labor_strength: The party wants action now. ?][? if unemployment_insurance_crisis > 1: We can't prolong this any longer. ?]
choose-if: reformist_strength + center_strength >= left_strength + labor_strength / 2 and unemployment_insurance_crisis < 2
on-arrival: workers_spd -= 0.1*workers_spd; unemployed_spd -= 0.15*unemployed_spd; workers_kpd += 2; workers_nsdap += 2; unemployed_kpd += 4; unemployed_nsdap += 4; left_dissent += 12; center_dissent += 7; labor_dissent += 12; reformist_dissent += 3; welfare -= 1; unemployed += 1; pro_republic -= 2; kpd_relation -= 8; inflation -= 1; ddp_cohesion -= 0.5; ddp_right += 1; prolonged_unemployment_insurance = 1; unemployment_insurance_timer = 6; goal_spd_cancel += 1; economic_growth -= 0.2

A compromise put forward by Heinrich Brüning is put into place and the issue is delayed until the autumn. A fixed amount is loaned to the program in the meantime, although not nearly enough for the millions of unemployed. The Left and Labor factions view this as a betrayal of our duty to support unemployment insurance during a crisis.

@compromise
unavailable-subtitle: [? if dvp_relation < 30 or ddp_relation < 30 or (z_relation < 30 or (z_relation < 25 and z_leader == "Joos") or (z_relation < 20 and z_leader == "Kaiser")): We do not have good enough relations with the right-wing parties. ?]
choose-if: dvp_relation >= 30 and ddp_relation >= 30 and (z_relation >= 30 or (z_relation >= 25 and z_leader == "Joos") or (z_relation >= 20 and z_leader == "Kaiser"))

The <span style="color: #C0A054;">**DVP**</span> ministers in the cabinet have offered a compromise that raises some taxes on the wealthy, along with lesser welfare cuts. Our members have mixed opinions on accepting this compromise. Reformists and Centrists support maintaining our government to keep the <span style="color: #7A3C00;">fascists</span> and <span style="color: #700000;">communists</span> out, while Labor generally opposes any cuts, and the Left is opposed to compromises with the bourgeoisie.

The <span style="color: #C0A054;">**DVP**</span> themselves are split about this, the Reichstag delegation may go against the compromise fashioned by their own ministers. [? if dvp_ideology == "Left": However, the <span style="color: #C0A054;">**DVP**</span> has resisted the national shift towards the right for the most part. ?]If we do go ahead with this, the coalition hinges on the decision of the <span style="color: #C0A054;">**DVP**</span>.

- @accept_compromise: Accept the compromise.
- @destroy: Reject the compromise and let this government fall.
- @prolong: Decide this at a later date.

@accept_compromise
go-to: compromised_failed if (dvp_ideology == "Moderate" and dvp_relation < 55) or dvp_ideology == "Right"; compromise_success if dvp_ideology == "Left" or (dvp_ideology == "Moderate" and dvp_relation >= 55)


@compromised_failed

We have accepted the compromise offered by the bourgeoisie. However, German industry has lobbied especially hard to foment the <span style="color: #C0A054;">**DVP**</span>'s split from the Grand coalition, and the compromise is voted down within the <span style="color: #C0A054;">**DVP**</span> itself.

- @destroy: At least we tried, let this government fall.
- @prolong: Decide this at a later date.

@compromise_success
unavailable-subtitle: [? if reformist_strength + center_strength < left_strength + labor_strength: The party won't allow this. ?]
choose-if: reformist_strength + center_strength >= left_strength + labor_strength
on-arrival: workers_spd -= 5; unemployed_spd -= 6; workers_kpd += 3; unemployed_kpd += 3; unemployed_nsdap += 3; left_dissent += 10; labor_dissent += 8; center_dissent += 5; welfare -= 1; budget += 1; kpd_relation -= 4; inflation -= 1; dvp_left += 3; old_middle_dvp -= 3; new_middle_dvp -= 2; rural_dvp -= 2; old_middle_dnvp += 3 if dnvp_ideology == "Moderate"; new_middle_dnvp += 2 if dnvp_ideology == "Moderate"; rural_dnvp += 2 if dnvp_ideology == "Moderate"; old_middle_kvp += 3 if kvp_formed; new_middle_kvp += 2 if kvp_formed; rural_kvp += 2 if kvp_formed; ui1_compromise = 1; ddp_cohesion += 1; ddp_left += 1; economic_growth -= 0.3

We have accepted the compromise offered by the bourgeoisie. Many of the workers and unemployed see this as a betrayal, although we present it as the successful aversion of a much worse alternative.

Despite the intense lobbying by German industry, the <span style="color: #C0A054;">**DVP**</span> have voted for the compromise through the narrowest of votes.
