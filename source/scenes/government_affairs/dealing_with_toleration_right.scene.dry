title: Dealing with Toleration
new-page: true
is-card: true
card-image: img/portraits/BrüningHeinrich.jpg
view-if: dealing_with_toleration_timer = 0 and spd_toleration and spd_toleration_right and chancellor_party == "Z"
on-arrival: dealing_with_toleration_timer += 6; month_actions += 1
tags: govt_affairs
#subtitle: Despite not being in government, we can still provide support.

= Dealing with Toleration

We are currently acting as external support to the government of Chancellor [+ chancellor +], although our influence is weakened by his inclusion of the <span style="color: #3E88B3;">**DNVP**</span>. How should we use our position?

- @break_toleration
- @urge_against_austerity
- @urge_against_austerity_limited
- @urge_paramilitary
- @improve_relations
- @new_grand_coalition
- @stay_course: Stay the course, for stability's sake.

@break_toleration
view-if: not constructive_vonc
title: End the toleration agreement with a vote of no confidence!

Are we sure about this? A vote of no confidence would trigger new elections, and potentially an increased vote-share for the extremist parties. [? if in_weimar_coalition_prussia or in_grand_coalition_prussia or in_new_grand_coalition_prussia or in_popular_front_prussia: The <span style="color: #000000;">Center</span> also threatens a no-confidence vote against us in Prussia, pressuring us to stay the course.?]

- @vonc: Yes, end this arrangement!
- @stay_course: No, we should stay the course.

@vonc
on-arrival: spd_in_government = 0; spd_toleration = 0; spd_toleration_right = 0; in_grand_coalition = 0; chancellor = "Brüning"; chancellor_party = "Z"; next_election_time = time + 3 if next_election_time > time+3; next_election_month = month + 3 if next_election_time >= time+3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; left_strength += 8; pro_republic -= 5; new_middle_spd -= 3; rural_spd -= 3; old_middle_spd -= 3; catholics_spd -= 3; reformist_dissent += 5; left_dissent -= 5; dvp_relation -= 4; z_relation -= 4; ddp_relation -= 4; lvp_relation -= 4; ddp_right += 1; dvp_right += 2
go-to: new_prussia_election if in_weimar_coalition_prussia or in_grand_coalition_prussia or in_new_grand_coalition_prussia or in_popular_front_prussia and ((center_right_coalition_prussia < 50 and dnvp_ideology == "Moderate") or dnvp_ideology == "Radical"); center_right_coalition_prussia if in_weimar_coalition_prussia or in_grand_coalition_prussia or in_new_grand_coalition_prussia or in_popular_front_prussia and center_right_coalition_prussia >= 50 and dnvp_ideology == "Moderate"

We have called for a vote of no confidence, and have brought down this government. Chancellor [+ chancellor +] currently rules as acting chancellor.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@new_prussia_election
on-arrival: z_relation -= 4; ddp_relation -= 4; lvp_relation -= 4; dvp_right += 3; ddp_right += 2; lvp_right += 2; pro_republic -= 3; new_middle_spd -= 2; rural_spd -= 2; old_middle_spd -= 2; catholics_spd -= 2; new_middle_nsdap += 2; old_middle_nsdap += 2; rural_nsdap += 2; catholics_nsdap += 2;
call: set_next_election_time_prussia

The <span style="color: #000000;">Center</span> has called a retaliatory vote in Prussia, forcing the dissolution of the Landtag for new elections.

New Prussian elections have been set for [+ next_election_month_prussia : month +] [+ next_election_year_prussia +].

A double dissolution has shaken the people's faith in democracy, with the <span style="color: #7A3C00;">Nazis</span> exploiting the instability to claim that democracy is unworkable, and that Germany needs a strong leader.

@center_right_coalition_prussia
on-arrival: in_center_right_coalition_prussia = 1; prussia_leader = "Stegerwald"; prussia_leader = "Brüning" if z_leader == "Stegerwald"; new_middle_spd -= 3; z_relation -= 4; ddp_relation -= 4; lvp_relation -= 4; dvp_right += 3; ddp_right += 2; pro_republic -= 3; prussian_police_loyalty -= 0.06; prussian_police_strength -= 8; 

The <span style="color: #000000;">Center</span> has called a retaliatory no confidence vote in Prussia, replacing our government with a center-right coalition composed of all parties to our right, except for the <span style="color: #003755;">**DNF**</span> and <span style="color: #7A3C00;">**NSDAP**</span>. While this is a setback, it is not the worst possible outcome, as new elections could have led to significant gains for extremist parties.

[? if z_leader != "Stegerwald": Adam Stegerwald?][? if z_leader == "Stegerwald": Heinrich Brüning?] of the <span style="color: #000000;">Center Party</span> is selected to be the Minister-President of Prussia.

@urge_against_austerity
title: Urge the coalition against austerity politics, and to increase social spending.
subtitle: This will be significantly more difficult with the <span style="color: #3E88B3;">DNVP</span> in government.
choose-if: budget >= 1
unavailable-subtitle: This can only happen with a budget surplus.
go-to: urge_success if z_relation >= 55 and ((ddp_relation >= 45 and not lvp_formed) or (lvp_relation >= 50 and lvp_formed)); urge_failure if z_relation < 55 or ((ddp_relation < 45 and not lvp_formed) or (lvp_relation < 50 and lvp_formed))

@urge_success
on-arrival: welfare += 1; budget -= 1; workers_spd += 5*(1-dissent); unemployed_spd += 5*(1-dissent); new_middle_ddp += 1; old_middle_ddp += 1; new_middle_dvp += 1.5; old_middle_dvp += 1.5; new_middle_other += 1.5; old_middle_other += 1.5; new_middle_lvp += 3; old_middle_lvp += 3; z_relation -= 3; dvp_relation -= 3; ddp_relation -= 3; lvp_relation -= 3; reformist_dissent -= 5; center_dissent -= 5; labor_dissent -= 5; dvp_right += 1; ddp_right += 0.5; lvp_right += 1; hindenburg_angry_bruning += 10; economic_growth += 0.2; unemployed -= 0.2; dnvp_angry += 1; dnvp_angry += 2 if spd_toleration_right

We have successfully urged the coalition parties to increase social spending! Unfortunately, the center-right parties increasingly see us as a nuisance, and might not wish to continue these negotiations. [? if president == "Hindenburg": President Hindenburg is also losing patience with Brüning for yielding to our demands.?]

@urge_failure
on-arrival: z_relation -= 3; dvp_relation -= 3; ddp_relation -= 3; lvp_relation -= 3

Unfortunately, our negotiations have met with failure. The center-right parties increasingly see us as a nuisance, and might not wish to continue these negotiations.

@urge_against_austerity_limited
title: Attempt to pressure Brüning to curb the worse excesses of his austerity policies. 
go-to: urge_success_limited if z_relation >= 45; urge_failure if z_relation < 45

@urge_success_limited
on-arrival: bruning_austerity_limited += 1; workers_spd += 3*(1-dissent); unemployed_spd += 3*(1-dissent); new_middle_spd += 3*(1-dissent) if bruning_austerity_limited >= 3; new_middle_ddp += 1; old_middle_ddp += 1; new_middle_dvp += 1; old_middle_dvp += 1; new_middle_other += 1; old_middle_other += 1; new_middle_nsdap -= 2; old_middle_nsdap -= 2; new_middle_lvp += 2; old_middle_lvp += 2; z_relation -= 3; reformist_dissent -= 3; center_dissent -= 3; labor_dissent -= 3; dvp_right += 1; ddp_right += 0.5; lvp_right += 1; hindenburg_angry_bruning += 5; economic_growth += 0.1; unemployed -= 0.1; dnvp_angry += 1 if spd_toleration_right

We have successfully pressured Brüning to tone down the severity of his austerity policies in his emergency decrees. Unfortunately, the center-right parties increasingly see us as a nuisance, and will attempt to force Brüning to form a majority coalition with the <span style="color: #7A3C00;">far</span>-<span style="color: #003755;">right</span> instead. [? if president == "Hindenburg": President Hindenburg is also losing patience with Brüning for yielding to our demands.?]

[? if bruning_austerity_limited == 1: We have successfully prevented Brüning from decreasing taxes on high earners, lessening the burden of compensatory tax increases on the lower strata. ?][? if bruning_austerity_limited == 2: We have successfully pressured Brüning into attempting to appease us by reinstating the 40-hour workweek, however wages themselves remains suboptimal. ?][? if bruning_austerity_limited >= 3: We have successfully pressured Brüning in reversing some salary cuts to civil servants. ?]

@urge_paramilitary
view-if: political_decree < 2
choose-if: spd_prussia and strife >= 3 and z_relation >= 35 and not spd_toleration_right
unavailable-subtitle: [? if not spd_prussia: We don't control the Prussian government. ?] [? if strife < 3: Paramilitary violence isn't severe enough to justify an emergency decree. ?] [? if z_relation < 35: We lack sufficient relations with Brüning. ?] [? if spd_toleration_right: The <span style="color: #3E88B3;">DNVP</span> won't allow Brüning to restrict the activities of their own paramilitary. ?]
title: Urge Brüning to grant us stronger actions against political extremists, and to restrict certain rights.
on-arrival: political_decree += 1; prussian_police_loyalty += 0.04; prussian_police_strength += 5; kpd_relation -= 6; rfb_militancy *= 0.9; rfb_strength *= 0.85; sa_militancy *= 0.96; sa_strength *= 0.96; sh_militancy *= 0.98; sh_militancy *= 0.98; workers_kpd -= 1; workers_nsdap -= 1; unemployed_kpd -= 3; unemployed_nsdap -= 3; new_middle_nsdap -= 2; hindenburg_angry_bruning += 5

Carl Severing and other Prussian officials have successfully urged Brüning to issue an emergency decree expanding police authority to suppress political extremism. The decree imposes restrictions on political meetings, organizations, newspapers, and posters, all of which now require police approval beforehand.

As a result, several <span style="color: #7A3C00;">Nazi</span> and <span style="color: #700000;">Communist</span> newspapers have been banned, triggering violent clashes between their supporters and the police. [? if president == "Hindenburg": President Hindenburg has reluctantly signed the decree, but is losing patience with Brüning for yielding to our demands. ?]

@improve_relations
title: Improve relations with the parties of the coalition.
on-arrival: z_relation += 4*(1-dissent); dvp_relation += 3*(1-dissent); ddp_relation += 3*(1-dissent); lvp_relation += 3*(1-dissent); left_dissent += 5; dvp_left += 1; ddp_left += 0.5; ddp_cohesion -= 0.5; lvp_left += 1; dnvp_angry += 1 if spd_toleration_right

We have increased our level of contact and discussions with the parties of the coalition, primarily the <span style="color: #000000;">Center Party</span> and [? if not lvp_formed:<span style="color: #C0A054;">**DVP**</span>?][? if lvp_formed:<span style="color: #FFCC00;">**LVP**</span>?]. The <span style="color: #3E88B3;">**DNVP**</span> does its best to obstruct our efforts at constructive cooperation with Brüning.

Our attempts to reach out to the center-right are met with disdain by leftists within our party.


@new_grand_coalition
title: Can we join the government in a new grand coalition?
view-if: neo_weimar_coalition >= 50
choose-if: ((z_relation >= 50 and dnvp_ideology == "Radical") or (z_relation >= 60 and dnvp_ideology == "Moderate")) and (((dvp_relation >= 30 and dnvp_ideology == "Radical") or (dvp_relation >= 40 and dnvp_ideology == "Moderate") and not lvp_formed) or (((lvp_relation >= 30 and dnvp_ideology == "Radical") or (lvp_relation >= 40 and dnvp_ideology == "Moderate")) and lvp_formed)) and (ddp_relation >= 40 and lvp_formed) and resources >= 2 and not spd_toleration_right
subtitle: We will need 2 resources to smooth out the process of forming the coalition. [? if dnvp_ideology == "Moderate": This will be more difficult due to the <span style="color: #3E88B3;">DNVP</span>'s influence on the coalition. ?]
unavailable-subtitle: [? if z_relation < 50 and dnvp_ideology == "Radical" : We do not have adequate relations with the <span style="color: #000000;">Center Party</span>. ?][? if lvp_relation < 40 and lvp_formed and dnvp_ideology == "Moderate": We do not have adequate relations with the <span style="color: #FFCC00;">**LVP**</span>. ?][? if lvp_relation < 30 and lvp_formed and dnvp_ideology == "Radical": We do not have adequate relations with the <span style="color: #FFCC00;">**LVP**</span>. ?][? if dvp_relation < 30 and dnvp_ideology == "Radical" and not lvp_formed: We do not have adequate relations with the <span style="color: #C0A054;">DVP</span>. ?][? if z_relation < 60 and dnvp_ideology == "Moderate" : We do not have adequate relations with the <span style="color: #000000;">Center Party</span>. ?][? if dvp_relation < 40 and dnvp_ideology == "Moderate" and not lvp_formed : We do not have adequate relations with the <span style="color: #C0A054;">DVP</span>. ?] [? if ddp_relation < 40 and not lvp_formed: We do not have adequate relations with the <span style="color: #D3C24D;">[+ ddp_name +]</span>. ?][? if resources < 2 : We do not have enough resources. ?][? if spd_toleration_right: The <span style="color: #3E88B3;">**DNVP**</span> will never allow this, and besides, Brüning prefers working with them over us. ?]
on-arrival: resources -= 2; spd_in_government = 1; in_grand_coalition = 1; spd_toleration = 0; spd_toleration_right = 0; left_dissent += 6; reformist_strength += 5; leverage = 20 if leverage >= 20
achievement: constitutional_coalition

Our negotiations have succeeded, and the <span style="color: #000000;">Center Party</span> and [? if not lvp_formed:<span style="color: #C0A054;">**DVP**</span>?][? if lvp_formed:<span style="color: #FFCC00;">**LVP**</span>?] have agreed to form a new coalition government with us!

We are now part of a "constitutional coalition" government that includes all parties except the extremists of the <span style="color: #700000;">**KPD**</span>, <span style="color: #3E88B3;">**DNVP**</span>, and <span style="color: #7A3C00;">**NSDAP**</span>. [+ chancellor +] will remain as chancellor. Leftists in our party are skeptical of this arrangement.

- @election_1928.ministries: Now, we must select our ministries.



@stay_course

We will stay the course of toleration, for the sake of stability.
